pkgbase='couldinho'
pkgname=(couldinho-base couldinho-desktop couldinho-laptop couldinho-sec)
pkgver=1
pkgrel=64
pkgdesc="Packages for couldinho systems"
arch=(x86_64)
url="https://github.com/kontax/dotfiles"
license=(MIT)
groups=(couldinho)

source=(conf-files.tar.xz)
sha256sums=(62dc3d5c7a6560e7f2ab721353fa987be125e7452c3dc6a299f2530a0577cf28)

package_couldinho-base() {
    install=couldinho-base.install

    install -Dm 0644 base/etc/profile.d/couldinho-base.sh "$pkgdir/etc/profile.d/couldinho-base.sh"

    # base
    depends=(
        bash
        bzip2
        coreutils
        device-mapper
        dhcpcd
        diffutils
        e2fsprogs
        file
        filesystem
        findutils
        gawk
        gcc-libs
        gettext
        glibc
        grep
        gzip
        inetutils
        iproute2
        iputils
        less
        licenses
        linux
        linux-firmware
        logrotate
        man-db
        man-pages
        mdadm
        netctl
        pacman
        pciutils
        perl
        procps-ng
        psmisc
        s-nail
        sed
        shadow
        sysfsutils
        systemd-sysvcompat
        tar
        texinfo
        usbutils
        util-linux
        which
    )

    # base-devel
    depends+=(
        autoconf
        automake
        binutils
        bison
        fakeroot
        flex
        gcc
        groff
        libtool
        m4
        make
        patch
        pkgconf
        sudo
        systemd
    )

    install -Dm 0644 base/etc/default/grub "$pkgdir/etc/default/couldinho-grub"
    install -Dm 0644 base/etc/mkinitcpio.conf "$pkgdir/etc/couldinho-mkinitcpio.conf"
    install -Dm 0644 base/etc/pacman.conf "$pkgdir/etc/couldinho-pacman.conf"
    install -Dm 0644 base/etc/pacman.d/couldinho-arch-aur "$pkgdir/etc/pacman.d/couldinho-arch-aur"
    install -Dm 0400 base/etc/sudoers "$pkgdir/etc/couldinho-sudoers"
    install -Dm 0644 base/etc/vimrc "$pkgdir/etc/couldinho-vimrc"
    install -Dm 0644 base/etc/vconsole.conf "$pkgdir/etc/vconsole.conf"
    install -Dm 0644 base/usr/share/kbd/keymaps/i386/qwerty/jc.map.gz \
                "$pkgdir/usr/share/kbd/keymaps/i386/qwerty/jc.map.gz"

    # Boot and base progs
    depends+=(
        grub efibootmgr     # For EFI systems
        cryptboot           # Encrypted partitions
        intel-ucode         # Microcode patches
        fwupd               # Firmware updater
        terminus-font       # Better font for console
        git                 # Move to devel?
        cifs-utils          # Connect to shared drives
        stow                # Link dotfiles to home dir
        neovim              # Vim replacement
        python-pynvim       # Python modules for neovim
        nodejs              # JS dev for use in coc.vim #TODO: Move to dev
        npm                 # Node package manager
    )

    install -Dm 0644 base/etc/gitconfig "$pkgdir/etc/gitconfig"
    install -Dm 0644 base/etc/xdg/nvim/init.vim "$pkgdir/etc/xdg/nvim/init.vim"
    install -Dm 0644 base/etc/xdg/nvim/bootstrap.vim \
        "$pkgdir/etc/xdg/nvim/bootstrap.vim"
    install -Dm 0644 base/etc/xdg/nvim/plugins.vim \
        "$pkgdir/etc/xdg/nvim/plugins.vim"
    install -dm 0775 "$pkgdir/etc/xdg/nvim/autoload"
    cp -r base/etc/xdg/nvim/modules "$pkgdir/etc/xdg/nvim/modules"

    # Base replacements
    depends+=(
        exa                 # ls replacement
        ripgrep             # Fuzzy search
        rmtrash             # Delete to bin
    )

    # Networking + VPN
    depends+=(
        networkmanager              # Networking
        networkmanager-strongswan   # IPSEC VPN
        iw                          # Move to laptop?
        wget openssh                # Download and connect
        openvpn                     # Connect to PIA
        networkmanager-openvpn      # NetworkManager drivers for OpenVPN
        private-internet-access-vpn # Scripts for managing PIA
        gnu-netcat                  # GNU netcat - contains -e for executables
        #openbsd-netcat             # OpenBSD netcat #TODO: Find a way to install both
        socat                       # Similar to netcat
    )

    install -Dm 0644 base/etc/private-internet-access/pia.conf "$pkgdir/etc/private-internet-access/pia.conf"

    # Shell
    depends+=(
        zsh                 # Better shell
        antigen-git         # Package manager
        prezto-git          # Configuration framework
        fzy                 # Fzy text selector
        lscolors-git        # Colours for ls
        pydf                # Colourised version of df
    )

    install -Dm 0644 base/etc/zsh/antigen.zsh "$pkgdir/etc/zsh/antigen.zsh"
    install -Dm 0644 base/etc/zsh/aliases.zsh "$pkgdir/etc/zsh/aliases.zsh"
    install -Dm 0644 base/etc/zsh/env.zsh "$pkgdir/etc/zsh/env.zsh"
    install -Dm 0644 base/etc/zsh/fuzzy.zsh "$pkgdir/etc/zsh/fuzzy.zsh"
    install -Dm 0644 base/etc/zsh/git.zsh "$pkgdir/etc/zsh/git.zsh"
    install -Dm 0644 base/etc/zsh/keybindings.zsh "$pkgdir/etc/zsh/keybindings.zsh"
    install -Dm 0644 base/etc/zsh/opts.zsh "$pkgdir/etc/zsh/opts.zsh"
    install -Dm 0644 base/etc/zsh/pacman.zsh "$pkgdir/etc/zsh/pacman.zsh"
    install -Dm 0644 base/etc/zsh/prezto.zsh "$pkgdir/etc/zsh/prezto.zsh"
    install -Dm 0644 base/etc/zsh/prezto-patches.zsh "$pkgdir/etc/zsh/prezto-patches.zsh"
    install -Dm 0644 base/etc/zsh/prompt.zsh "$pkgdir/etc/zsh/prompt.zsh"
    install -Dm 0644 base/etc/zsh/sandboxd.zsh "$pkgdir/etc/zsh/sandboxd.zsh"
    install -Dm 0644 base/etc/zsh/ssh.zsh "$pkgdir/etc/zsh/ssh.zsh"
    install -Dm 0644 base/etc/zsh/title.zsh "$pkgdir/etc/zsh/title.zsh"
    install -Dm 0644 base/etc/zsh/zsh-notify.zsh "$pkgdir/etc/zsh/zsh-notify.zsh"
    install -Dm 0644 base/etc/zsh/zshrc "$pkgdir/etc/zsh/couldinho-zshrc"
    install -Dm 0644 base/etc/zsh/zprezto-patches/prompt.patch "$pkgdir/etc/zsh/zprezto-patches/prompt.patch"
    install -Dm 0644 base/etc/zsh/zprezto-patches/wordchars.patch \
                 "$pkgdir/etc/zsh/zprezto-patches/wordchars.patch"

    # Filesystem
    depends+=(
        btrfs-progs         # BTRFS filesystem tools
        snapper             # Snapshot manager
        snap-pac            # Hooks for pacman for auto-snapshotting
        snap-pac-grub       # Add grub entries for snapshots
        zip                 # Zipping tool
        unzip               # Unzipping tool
    )

    install -Dm 0644 base/etc/conf.d/snapper "$pkgdir/etc/conf.d/couldinho-snapper"
    install -Dm 0644 base/etc/snapper/configs/root "$pkgdir/etc/snapper/configs/root"
    install -Dm 0644 base/etc/snap-pac.conf "$pkgdir/etc/snap-pac.conf"
    install -Dm 0644 base/etc/default/grub-btrfs/config "$pkgdir/etc/default/grub-btrfs/couldinho-config"

    # Access Control
    depends+=(
        pass                # Text-based password manager
        pass-git-helper     # Credential helper using pass to store details
        usbguard            # USB security configuration
        yubikey-manager     # Yubikey settings
        yubico-pam          # PAM settings for yubikey
        libu2f-server       # Libraries for U2F - login with yubikey
        pam-u2f             # Login to system with U2F
        git-secret          # Helper tool for storing private files in a git repo
    )

    install -Dm 0644 base/etc/profile.d/zz_custom.sh "$pkgdir/etc/profile.d/zz_custom.sh"
    install -dm 0750 base/etc/usbguard "$pkgdir/etc/usbguard"
    install -Dm 0600 base/etc/usbguard/usbguard-daemon.conf \
        "$pkgdir/etc/usbguard/couldinho-usbguard-daemon.conf"

    # Packages
    depends+=(
        devtools            # Package building helpers
        aurutils-git        # AUR helper
        pacman-contrib      # Contains checkupdates plus other pacman scripts
        reflector           # Automatically sort out pacman mirrorlist
    )

    install -Dm 0644 base/etc/systemd/system/reflector.service "$pkgdir/etc/systemd/system/reflector.service"
    install -Dm 0644 base/etc/systemd/system/reflector.timer "$pkgdir/etc/systemd/system/reflector.timer"
}

package_couldinho-desktop() {
    install=couldinho-desktop.install

    install -Dm 0644 desktop/etc/profile.d/couldinho-desktop.sh "$pkgdir/etc/profile.d/couldinho-desktop.sh"

    # base
    depends=(
        couldinho-base
    )

    # xorg
    depends+=(
        xorg-server         	# X server
        xorg-xfontsel       	# Selecting X11 fonts easily
        xorg-xinit          	# Start up X and various WM's
        xorg-xprop          	# Display X properties
        xorg-xrandr         	# Resize & Rotate
    )

    install -Dm 0644 desktop/etc/X11/xorg.conf.d/00-keyboard.conf \
                    "$pkgdir/etc/X11/xorg.conf.d/00-keyboard.conf"
    install -Dm 0644 desktop/usr/share/X11/xkb/symbols/jc "$pkgdir/usr/share/X11/xkb/symbols/jc"

    # i3
    depends+=(
        i3-gaps                 # i3 window manager with gaps between windows
        i3status                # Status bar for i3
        py3status               # Manage i3status through python
        python-i3ipc            # IPC for i3 via python
    )

    install -Dm 0644 desktop/etc/i3/config "$pkgdir/etc/i3/couldinho-config"
    install -Dm 0644 desktop/etc/i3status.conf "$pkgdir/etc/couldinho-i3status.conf"
    install -Dm 0644 desktop/usr/share/fonts/taskbar.ttf "$pkgdir/usr/share/fonts/taskbar.ttf"

    # Login manager
    depends+=(
        lightdm             	# Login manager
        lightdm-gtk-greeter 	# Theme for login manager
    )

    install -Dm 0644 desktop/etc/lightdm/lightdm.conf "$pkgdir/etc/lightdm/couldinho-lightdm.conf"
    install -Dm 0644 desktop/etc/lightdm/lightdm-gtk-greeter.conf \
        "$pkgdir/etc/lightdm/couldinho-lightdm-gtk-greeter.conf"

    # Further i3 packages
    depends+=(
        rofi                	# dmenu replacement
        rofi-calc           	# Wrapper for qalculate
        libqalculate        	# More powerful calculator
        rofi-dmenu          	# Wrapper for things depending on dmenu binary
        ttf-inconsolata     	# Font for terminal and status etc.
        otf-font-awesome    	# Font with icons
        python-tzlocal      	# Used within the clock for the status bar
        gsimplecal          	# GTK calendar for the status bar
        dunst               	# Notification server
        libnotify               # Notification library
        python-pydbus           # Python DBus connector (used by usbguard i3status module)
        yubikey-touch-detector  # i3status notification for when yubikey needs attention 
        pinentry-dmenu          # Enter GPG pins using dmenu
    )

    install -Dm 0644 desktop/etc/rofi.conf "$pkgdir/etc/rofi.conf"

    # Multimedia
    depends+=(
        alsa-utils          	# Sound support
        pulseaudio          	# Sound server
        pulseaudio-alsa     	# ALSA config for pulseaudio
        pamixer             	# Command line mixer
    )

    # Documents
    depends+=(
        zathura                 # PDF viewer with vim bindings
        zathura-pdf-mupdf       # MuPDF plugin for zathura
    )

    # Terminal + desktop tools
    depends+=(
        kitty               	# GPU enhanced terminal emulator
        flameshot           	# Screenshot tool
        picom                	# X compositor
        copyq               	# Copy/Paste utility
        unclutter           	# Hide mouse in i3 when not in use
        ranger              	# File manager from terminal (vim keybindings)
        python-pillow       	# Used to view pictures within ranger
        freerdp             	# Remote desktop client
        xsecurelock             # Desktop lock screen
    )

    install -Dm 0644 desktop/etc/xdg/kitty/diff.conf "$pkgdir/etc/xdg/kitty/diff.conf"
    install -Dm 0644 desktop/etc/xdg/kitty/kitty.conf "$pkgdir/etc/xdg/kitty/kitty.conf"
    install -Dm 0644 desktop/etc/xdg/kitty/vm.py "$pkgdir/etc/xdg/kitty/vm.py"
    install -Dm 0644 desktop/etc/xdg/picom.conf "$pkgdir/etc/xdg/couldinho-picom.conf"

    # Networking
    depends+=(
        nm-connection-editor        # Gui tool for managing NetworkManager connections
        networkmanager-dmenu-git    # dmenu for NetworkManager connections
    )

    # Web
    depends+=(
        vivaldi                 # Browser
        vivaldi-ffmpeg-codecs   # Browser video codecs
        vivaldi-widevine        # Stream DRM support for vivaldi
        pepper-flash            # Flash player
        x265                    # Video compression
        browserpass-chromium    # Browser extenson for pass
    )

    # Utils
    depends+=(
        #wire-desktop-beta       # Encrypted messenger
    )

    # General scripts
    install -Dm 0755 desktop/usr/local/bin/check-aur-became-official "$pkgdir/usr/local/bin/check-aur-became-official"
    install -Dm 0755 desktop/usr/local/bin/i3-autoname-workspaces "$pkgdir/usr/local/bin/i3-autoname-workspaces"
    install -Dm 0755 desktop/usr/local/bin/i3exit "$pkgdir/usr/local/bin/i3exit"
    install -Dm 0755 desktop/usr/local/bin/is_online "$pkgdir/usr/local/bin/is_online"
    install -Dm 0755 desktop/usr/local/bin/rdp "$pkgdir/usr/local/bin/rdp"
    install -Dm 0755 desktop/usr/local/bin/renumber-sessions "$pkgdir/usr/local/bin/renumber-sessions"
    install -Dm 0755 desktop/usr/local/bin/rofi-calc "$pkgdir/usr/local/bin/rofi-calc"
    install -Dm 0755 desktop/usr/local/bin/set-brightness "$pkgdir/usr/local/bin/set-brightness"
}

package_couldinho-dev() {
    install=couldinho-dev.install

    # No dotfile scripts are installed here - use the next dependency
    #install -Dm 0644 dev/etc/profile.d/couldinho-dev.sh "$pkgdir/etc/profile.d/couldinho-dev.sh"

    depends=(
        couldinho-base
        couldinho-desktop
    )

    # Platforms/Languages
    depends+=(
        docker                  # Containerisation
        rust                    # The rust programming language and tools
        rustup                  # Rust toolchain installer
        nasm                    # x86 Assembler
    )

    # IDE's
    depends+=(
        pycharm-professional    # Python IDE by JetBrains
    )

    # Debugging
    depends+=(
        gdb                     # GNU debugger
        rr                      # Replaying debugger
    )

    # Cloud
    depends+=(
        aws-cli                 # Command line interface to AWS
        npm                     # Node package manager (used for serverless)
    )
}

package_couldinho-sec() {
    install=couldinho-sec.install

    # No dotfile scripts are installed here - use the next dependency
    #install -Dm 0644 sec/etc/profile.d/couldinho-sec.sh "$pkgdir/etc/profile.d/couldinho-sec.sh"

    depends=(
        couldinho-desktop
    )

    # Binary
    depends+=(
        010editor               # Binary editor with templates
        ltrace                  # Trace library calls
        strace                  # Trace system calls and signals
        binwalk                 # Find and extract file types within a file
    )

    # Network
    depends+=(
        nmap                    # Network mapper
        wireshark-cli           # Network analyzer - CLI tools
        wireshark-qt            # Network analyzer - GUI
        burpsuite               # Security testing web applications
        dirbuster               # Brute force directories and filenames
        wfuzz                   # Brute force web applications
    )

    # Reversing
    depends+=(
        radare2                 # Command line reverse engineering framework
        ida-free                # Hexrays reversen engineering framework
        ghidra-bin              # Reverse engineering framework similar to IDA
        voltron-git             # A more useful display for GDB
        gef-git                 # GDB Enhanced Features
        python-capstone         # GEF optional dependency
        python-unicorn          # GEF optional dependency
        python-keystone         # GEF optional dependency
        python-ropper           # GEF optional dependency
    )

    # Cracking
    depends+=(
        john                    # John the Ripper password cracker
    )
}

package_couldinho-laptop() {
    install=couldinho-laptop.install

    install -Dm 0644 laptop/etc/profile.d/couldinho-laptop.sh "$pkgdir/etc/profile.d/couldinho-laptop.sh"

    # base + desktop
    depends=(
        couldinho-desktop
    )

    # Laptop specific programs
    depends+=(
        light                   # Program to change backlight
    )

    # Overwrite desktop files
    install -Dm 0644 laptop/etc/i3status.conf "$pkgdir/etc/couldinho-laptop-i3status.conf"
    install -Dm 0644 laptop/etc/i3/config "$pkgdir/etc/i3/couldinho-laptop-config"
    install -Dm 0644 laptop/etc/lightdm/lightdm-gtk-greeter.conf \
        "$pkgdir/etc/lightdm/couldinho-laptop-lightdm-gtk-greeter.conf"
    install -Dm 0644 laptop/etc/rofi.conf "$pkgdir/etc/couldinho-laptop-rofi.conf"
    install -Dm 0644 laptop/etc/xdg/kitty/kitty.conf "$pkgdir/etc/xdg/kitty/couldinho-laptop-kitty.conf"
    install -Dm 0644 laptop/usr/share/X11/xorg.conf.d/40-libinput.conf \
        "$pkgdir/usr/share/X11/xorg.conf.d/couldinho-laptop-40-libinput.conf"
}
