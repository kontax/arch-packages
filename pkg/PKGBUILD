pkgbase='couldinho'
pkgname=(couldinho-base couldinho-desktop couldinho-laptop couldinho-dev couldinho-sec couldinho-vmware)
pkgver=3
pkgrel=22
pkgdesc="Packages for couldinho systems"
arch=(x86_64)
url="https://github.com/kontax/arch-packages"
license=(MIT)
groups=(couldinho)

source=(conf-files.tar.xz)
sha256sums=(2c6cef289100e74b98a14e26332357329a22e19ce8313d8720bca6b015963b71)

# Comment for update

package_couldinho-base() {
    install=couldinho-base.install

    # base
    depends=(
        bash
        bzip2
        coreutils
        dash
        device-mapper
        dhcpcd
        diffutils
        e2fsprogs
        file
        filesystem
        findutils
        gawk
        gcc-libs
        gettext
        glibc
        grep
        gzip
        inetutils
        iproute2
        iputils
        less
        licenses
        linux
        linux-firmware
        logrotate
        man-db
        man-pages
        mdadm
        netctl
        pacman
        pciutils
        perl
        procps-ng
        psmisc
        s-nail
        sed
        shadow
        sysfsutils
        systemd-sysvcompat
        tar
        texinfo
        usbutils
        util-linux
        which
    )

    # base-devel
    depends+=(
        autoconf
        automake
        binutils
        bison
        fakeroot
        flex
        gcc
        groff
        libtool
        linux-headers
        m4
        make
        patch
        pkgconf
        sudo
        systemd
    )

    install -Dm 0644 base/etc/mkinitcpio.conf "$pkgdir/etc/couldinho-mkinitcpio.conf"
    install -Dm 0644 base/etc/pacman.conf "$pkgdir/etc/couldinho-pacman.conf"
    install -Dm 0644 base/etc/pacman.d/couldinho-arch-aur "$pkgdir/etc/pacman.d/couldinho-arch-aur"
    install -Dm 0400 base/etc/sudoers "$pkgdir/etc/couldinho-sudoers"
    install -Dm 0644 base/etc/vimrc "$pkgdir/etc/couldinho-vimrc"
    install -Dm 0644 base/etc/xkb/symbols/us-hyper "$pkgdir/etc/xkb/symbols/us-hyper"

    # Boot and base progs
    depends+=(
        cryptboot           # Encrypted partitions
        mkinitcpio-encrypt-detached-header # Encrypt hook with detatched header
        arch-secure-boot    # UEFI Secure Boot
        intel-ucode         # Microcode patches
        fwupd               # Firmware updater
        terminus-font       # Better font for console
        git                 # Move to devel?
        cifs-utils          # Connect to shared drives
        stow                # Link dotfiles to home dir
        neovim              # Vim replacement
        python-pynvim       # Python modules for neovim
        nodejs              # JS dev for use in coc.vim #TODO: Move to dev
        npm                 # Node package manager
    )

    install -Dm 0644 base/etc/gitconfig "$pkgdir/etc/gitconfig"
    install -Dm 0644 base/etc/xdg/nvim/init.vim "$pkgdir/etc/xdg/nvim/init.vim"
    install -Dm 0644 base/etc/xdg/nvim/bootstrap.vim \
        "$pkgdir/etc/xdg/nvim/bootstrap.vim"
    install -Dm 0644 base/etc/xdg/nvim/plugins.vim \
        "$pkgdir/etc/xdg/nvim/plugins.vim"
    install -dm 0775 "$pkgdir/etc/xdg/nvim/autoload"
    cp -r base/etc/xdg/nvim/modules "$pkgdir/etc/xdg/nvim/modules"

    # Base replacements
    depends+=(
        exa                 # ls replacement
        ripgrep             # Fuzzy search
        ripgrep-all         # ripgrep wrapper for multiple file types
        rmtrash             # Delete to bin
        bat                 # Cat replacement with syntax highlighting
        htop                # Improved top - process viewer
    )

    install -dm 0755 base/etc/bat "$pkgdir/etc/bat"
    install -Dm 0644 base/etc/htoprc "$pkgdir/etc/htoprc"

    # Networking + VPN
    depends+=(
        networkmanager              # Networking
        networkmanager-strongswan   # IPSEC VPN
        iw                          # Move to laptop?
        wget openssh                # Download and connect
        openvpn                     # Connect to PIA
        networkmanager-openvpn      # NetworkManager drivers for OpenVPN
        #gnu-netcat                 # GNU netcat - contains -e for executables
        openbsd-netcat              # OpenBSD netcat #TODO: Find a way to install both
        socat                       # Similar to netcat
        bandwhich                   # Terminal bandwith utilization tool
        croc                        # Send files securely to another computer
        speedtest-cli               # Test network speed
        net-tools                   # Network configuration tools
    )

    # Shell
    depends+=(
        zsh                 # Better shell
        direnv              # Shell extension managing environment
        fzy                 # Fzy text selector
        lscolors-git        # Colours for ls
        dfrs                # Colourised version of df
        tmux                # Terminal multiplexer
        fzf                 # Command line fuzzy finder
        bash-completion     # Auto-complete for bash
    )

    install -Dm 0644 base/etc/zsh/zshrc "$pkgdir/etc/zsh/couldinho-zshrc"
    install -Dm 0644 base/etc/zsh/p10k.zsh "$pkgdir/etc/zsh/couldinho-p10k.zsh"
    install -Dm 0644 base/etc/zsh/zprofile "$pkgdir/etc/zsh/couldinho-zprofile"
    install -Dm 0644 base/etc/zsh/zsh-aliases "$pkgdir/etc/zsh/couldinho-zsh-aliases"
    install -Dm 0644 base/etc/zsh/zshenv "$pkgdir/etc/zsh/couldinho-zshenv"

    # Filesystem
    depends+=(
        btrfs-progs         # BTRFS filesystem tools
        snapper             # Snapshot manager
        snap-pac            # Hooks for pacman for auto-snapshotting
        zip                 # Zipping tool
        unzip               # Unzipping tool
        ncdu                # NCurses version of du
    )

    install -Dm 0644 base/etc/conf.d/snapper "$pkgdir/etc/conf.d/couldinho-snapper"
    install -Dm 0644 base/etc/snapper/configs/root "$pkgdir/etc/snapper/configs/root"
    install -Dm 0644 base/etc/snap-pac.conf "$pkgdir/etc/snap-pac.conf"

    # Access Control + Security
    depends+=(
        pass                # Text-based password manager
        pass-git-helper     # Credential helper using pass to store details
        pwgen               # Password generator
        usbguard            # USB security configuration
        yubikey-manager     # Yubikey settings
        yubico-pam          # PAM settings for yubikey
        libu2f-server       # Libraries for U2F - login with yubikey
        pam-u2f             # Login to system with U2F
        git-secret          # Helper tool for storing private files in a git repo
        arch-audit          # View security details of arch packages
        earlyoom            # Detect and handle OOM
    )

    install -Dm 0644 base/etc/pacman.d/hooks/arch-audit.hook "$pkgdir/etc/pacman.d/hooks/arch-audit.hook"
    install -dm 0750 base/etc/usbguard "$pkgdir/etc/usbguard"
    install -Dm 0600 base/etc/usbguard/usbguard-daemon.conf \
        "$pkgdir/etc/usbguard/couldinho-usbguard-daemon.conf"
    install -Dm 0644 base/etc/default/earlyoom "$pkgdir/etc/default/couldinho-earlyoom"

    # Packages
    depends+=(
        devtools            # Package building helpers
        aurutils-git        # AUR helper
        pacman-contrib      # Contains checkupdates plus other pacman scripts
        reflector           # Automatically sort out pacman mirrorlist
        urlwatch            # Tool for monitoring webpage updates
        python-chump-git    # Pushover dependency for urlwatch
        rebuild-detector    # Check which packages need to be rebuilt
    )

    install -Dm 0644 base/etc/systemd/system/reflector.service "$pkgdir/etc/systemd/system/reflector.service"
    install -Dm 0644 base/etc/systemd/system/reflector.timer "$pkgdir/etc/systemd/system/reflector.timer"

    # Environment
    install -Dm 0644 base/etc/environment.d/10-locale.conf "$pkgdir/etc/environment.d/10-locale.conf"
    install -Dm 0644 base/etc/environment.d/30-path.conf "$pkgdir/etc/environment.d/30-path.conf"
    install -Dm 0644 base/etc/environment.d/40-clean-home.conf "$pkgdir/etc/environment.d/40-clean-home.conf"
    install -Dm 0644 base/etc/environment.d/50-progs.conf "$pkgdir/etc/environment.d/50-progs.conf"
    install -Dm 0644 base/etc/environment.d/90-pass.conf "$pkgdir/etc/environment.d/90-pass.conf"
}

package_couldinho-desktop() {
    install=couldinho-desktop.install

    # base
    depends=(
        couldinho-base
    )

    # backend
    depends+=(
        xorg-xwayland           # X server
        qt5-wayland             # QT API's for Wayland
        vulkan-intel            # Intel Vulkan mesa driver #TODO: Is this required?
        vulkan-headers          # Vulkan header files
    )

    install -Dm 0644 desktop/etc/X11/xorg.conf.d/00-keyboard.conf \
                    "$pkgdir/etc/X11/xorg.conf.d/00-keyboard.conf"

    # sway
    depends+=(
        sway                    # Tiling wayland compositor replacing i3
        swaylock                # Lock wayland screen
        wl-clipboard            # Wayland command-line clipboard manager
        python-i3ipc            # IPC for i3 via python
        waybar                  # Sway bar for wayland
        slurp                   # Region selector for wayland
        swayr                   # Window switcher for sway
        swaync                  # Sway notification daemon
        libnotify               # Desktop notification library
        rofi                    # Launcher
    )

    install -Dm 0644 desktop/etc/sway/config \
        "$pkgdir/etc/sway/couldinho-desktop-config"
    install -Dm 0644 desktop/usr/share/fonts/taskbar.ttf \
        "$pkgdir/usr/share/fonts/taskbar.ttf"
    install -Dm 0644 desktop/etc/swaylock/config \
        "$pkgdir/etc/swaylock/config"
    install -Dm 0644 desktop/etc/X11/xorg.conf.d/00-keyboard.conf \
        "$pkgdir/etc/X11/xorg.conf.d/00-keyboard.conf"
    install -Dm 0644 desktop/etc/xdg/waybar/config \
        "$pkgdir/etc/xdg/waybar/couldinho-desktop-config"
    install -Dm 0644 desktop/etc/xdg/waybar/style.css \
        "$pkgdir/etc/xdg/waybar/couldinho-desktop-style.css"
    install -Dm 0644 desktop/etc/xdg/swaync/config.json \
        "$pkgdir/etc/xdg/swaync/couldinho-desktop-config.json"
    install -Dm 0644 desktop/etc/xdg/swaync/style.css \
        "$pkgdir/etc/xdg/swaync/couldinho-desktop-style.css"
    install -Dm 0644 desktop/etc/systemd/user/wl-clipboard-manager.service \
        "$pkgdir/etc/systemd/user/wl-clipboard-manager.service"
    install -Dm 0644 desktop/etc/zsh/zprofile \
        "$pkgdir/etc/zsh/couldinho-desktop-zprofile"
    install -Dm 0644 desktop/etc/rofi.rasi "$pkgdir/etc/rofi.rasi"

    # Desktop mimetype apps
    install -Dm 0644 desktop/etc/xdg/mimeapps.list \
        "$pkgdir/etc/xdg/mimeapps.list"
    install -Dm 0644 desktop/usr/local/share/applications/browser.desktop \
        "$pkgdir/usr/local/share/applications/browser.desktop"
    install -Dm 0644 desktop/usr/local/share/applications/discord.desktop \
        "$pkgdir/usr/local/share/applications/discord.desktop"
    install -Dm 0644 desktop/usr/local/share/applications/neomutt.desktop \
        "$pkgdir/usr/local/share/applications/neomutt.desktop"
    install -Dm 0644 desktop/usr/local/share/applications/nvim.desktop \
        "$pkgdir/usr/local/share/applications/nvim.desktop"
    install -Dm 0644 desktop/usr/local/share/applications/scli.desktop \
        "$pkgdir/usr/local/share/applications/scli.desktop"
    install -Dm 0644 desktop/usr/local/share/applications/signal-desktop.desktop \
        "$pkgdir/usr/local/share/applications/signal-desktop.desktop"
    install -Dm 0644 desktop/usr/local/share/applications/visidata.desktop \
        "$pkgdir/usr/local/share/applications/visidata.desktop"

    # Further sway packages
    depends+=(
        flashfocus-git              # Focus animations
        qalculate-gtk               # Calculator
        ttf-inconsolata             # Font for terminal and status etc.
        otf-font-awesome            # Font with icons
        ttf-joypixels               # Fonts with emojis
        python-tzlocal              # Used within the clock for the status bar
        gsimplecal                  # GTK calendar for the status bar
        inotify-tools               # Interface to inotify
        systembus-notify            # Systembus notification demon
        yubikey-touch-detector      # waybar notification for when yubikey needs attention 
        pinentry                    # Enter GPG pins using dmenu
        gtk-theme-arc-gruvbox-git   # Arc grey based theme with some gruvboxd colours
        rofi-pass-git               # Rofi-based pass handler
        qrencode                    # Encode passwords into QR codes
    )

    install -Dm 0644 desktop/etc/gtk-3.0/settings.ini \
        "$pkgdir/etc/gtk-3.0/settings.ini"
    install -Dm 0644 desktop/etc/xdg/flashfocus/flashfocus.yml \
        "$pkgdir/etc/xdg/flashfocus/couldinho-desktop-flashfocus.yml"
    install -Dm 0644 desktop/etc/systemd/user/sway-autoname-workspaces.service \
        "$pkgdir/etc/systemd/user/sway-autoname-workspaces.service"
    install -Dm 0644 desktop/etc/systemd/user/sway-inactive-window-transparency.service \
        "$pkgdir/etc/systemd/user/sway-inactive-window-transparency.service"
    install -Dm 0644 desktop/etc/systemd/user/sway-session.target \
        "$pkgdir/etc/systemd/user/sway-session.target"
    install -Dm 0644 desktop/etc/systemd/user/swayr.service \
        "$pkgdir/etc/systemd/user/swayr.service"
    install -Dm 0644 desktop/etc/systemd/user/systembus-notify.service \
        "$pkgdir/etc/systemd/user/systembus-notify.service"
    install -Dm 0644 desktop/etc/systemd/user/waybar-updates.service \
        "$pkgdir/etc/systemd/user/waybar-updates.service"
    install -Dm 0644 desktop/etc/systemd/user/waybar-updates.timer \
        "$pkgdir/etc/systemd/user/waybar-updates.timer"
    install -Dm 0644 desktop/etc/systemd/user/waybar.service \
        "$pkgdir/etc/systemd/user/waybar.service"
    install -Dm 0644 desktop/etc/rofi-pass.conf \
        "$pkgdir/etc/couldinho-desktop-rofi-pass.conf"

    # Multimedia
    depends+=(
        alsa-utils              # Sound support
        pulseaudio              # Sound server
        pulseaudio-alsa         # ALSA config for pulseaudio
        pavucontrol             # PulseAudio volume control/
        pamixer                 # Command line mixer
        vimiv                   # Image viewer with vim bindings
        feh                     # Lightweight image viewer which handles stdin
        imagemagick             # Image viewing/manipulation program
        wf-recorder             # Wayland screen recorder
        mpv                     # Media player
        mpv-mpris               # MPRIS plugin for mpv (remote interface)
        yt-dlp                  # Download/play youtube videos
    )

    install -Dm 0644 desktop/etc/mpv/script-opts/osc.conf "$pkgdir/etc/mpv/script-opts/osc.conf"
    install -Dm 0644 desktop/etc/mpv/script-opts/uosc.conf "$pkgdir/etc/mpv/script-opts/uosc.conf"
    install -Dm 0644 desktop/etc/mpv/scripts/uosc.lua "$pkgdir/etc/mpv/scripts/uosc.lua"
    install -Dm 0644 desktop/etc/mpv/input.conf "$pkgdir/etc/mpv/input.conf"
    install -Dm 0644 desktop/etc/mpv/mpv.conf "$pkgdir/etc/mpv/mpv.conf"

    # Documents
    depends+=(
        zathura                 # PDF viewer with vim bindings
        zathura-pdf-mupdf       # MuPDF plugin for zathura
    )

    # Terminal + desktop tools
    depends+=(
        kitty                   # GPU enhanced terminal emulator
        slurp                   # Region selector tool
        grim                    # Screenshot utility
        swappy                  # Snapshot editing tool for wayland
        python-pillow           # Used to view pictures within ranger
        freerdp                 # Remote desktop client
        udiskie                 # Disk mounting tool
        udiskie-dmenu-git       # dmenu for udiskie
        gcr                     # Crypto UI library
        progress                # Shows progress for coreutils commands
    )

    install -Dm 0644 desktop/etc/xdg/kitty/diff.conf "$pkgdir/etc/xdg/kitty/diff.conf"
    install -Dm 0644 desktop/etc/xdg/kitty/kitty.conf "$pkgdir/etc/xdg/kitty/kitty.conf"
    install -Dm 0644 desktop/etc/xdg/kitty/vm.py "$pkgdir/etc/xdg/kitty/vm.py"
    install -Dm 0644 desktop/etc/xdg/swappy/config "$pkgdir/etc/xdg/swappy/config"
    install -Dm 0644 desktop/etc/systemd/user/udiskie.service "$pkgdir/etc/systemd/user/udiskie.service"

    # Networking
    #depends+=(
    #    nm-connection-editor        # Gui tool for managing NetworkManager connections
    #    networkmanager-dmenu-git    # dmenu for NetworkManager connections
    #)

    # Web
    depends+=(
        vivaldi                 # Browser
        vivaldi-ffmpeg-codecs   # Browser video codecs
        vivaldi-widevine        # Stream DRM support for vivaldi
        pepper-flash            # Flash player
        x265                    # Video compression
        browserpass-chromium    # Browser extenson for pass
    )

    # General scripts
    install -Dm 0755 desktop/usr/local/bin/browser "$pkgdir/usr/local/bin/browser"
    install -Dm 0755 desktop/usr/local/bin/cglaunch "$pkgdir/usr/local/bin/cglaunch"
    install -Dm 0755 desktop/usr/local/bin/checkofficial "$pkgdir/usr/local/bin/checkofficial"
    install -Dm 0755 desktop/usr/local/bin/dmenu "$pkgdir/usr/local/bin/dmenu"
    install -Dm 0755 desktop/usr/local/bin/dmenu-wl "$pkgdir/usr/local/bin/dmenu-wl"
    install -Dm 0755 desktop/usr/local/bin/emoji-bootstrap "$pkgdir/usr/local/bin/emoji-bootstrap"
    install -Dm 0755 desktop/usr/local/bin/emoji-dmenu "$pkgdir/usr/local/bin/emoji-dmenu"
    install -Dm 0755 desktop/usr/local/bin/rofi-nmcli "$pkgdir/usr/local/bin/rofi-nmcli"
    install -Dm 0755 desktop/usr/local/bin/pass-gen "$pkgdir/usr/local/bin/pass-gen"
    install -Dm 0755 desktop/usr/local/bin/waybar-decrypted "$pkgdir/usr/local/bin/waybar-decrypted"
    install -Dm 0755 desktop/usr/local/bin/waybar-dnd "$pkgdir/usr/local/bin/waybar-dnd"
    install -Dm 0755 desktop/usr/local/bin/waybar-mail "$pkgdir/usr/local/bin/waybar-mail"
    install -Dm 0755 desktop/usr/local/bin/waybar-movie "$pkgdir/usr/local/bin/waybar-movie"
    install -Dm 0755 desktop/usr/local/bin/waybar-progress "$pkgdir/usr/local/bin/waybar-progress"
    install -Dm 0755 desktop/usr/local/bin/waybar-recording "$pkgdir/usr/local/bin/waybar-recording"
    install -Dm 0755 desktop/usr/local/bin/waybar-systemd "$pkgdir/usr/local/bin/waybar-systemd"
    install -Dm 0755 desktop/usr/local/bin/waybar-updates "$pkgdir/usr/local/bin/waybar-updates"
    install -Dm 0755 desktop/usr/local/bin/waybar-usbguard "$pkgdir/usr/local/bin/waybar-usbguard"
    install -Dm 0755 desktop/usr/local/bin/waybar-vpn "$pkgdir/usr/local/bin/waybar-vpn"
    install -Dm 0755 desktop/usr/local/bin/waybar-yubikey "$pkgdir/usr/local/bin/waybar-yubikey"
    install -Dm 0755 desktop/usr/local/bin/sway-audio "$pkgdir/usr/local/bin/sway-audio"
    install -Dm 0755 desktop/usr/local/bin/sway-autoname-workspaces "$pkgdir/usr/local/bin/sway-autoname-workspaces"
    install -Dm 0755 desktop/usr/local/bin/sway-exit "$pkgdir/usr/local/bin/sway-exit"
    install -Dm 0755 desktop/usr/local/bin/sway-gif-area "$pkgdir/usr/local/bin/sway-gif-area"
    install -Dm 0755 desktop/usr/local/bin/sway-inactive-window-transparency "$pkgdir/usr/local/bin/sway-inactive-window-transparency"
    install -Dm 0755 desktop/usr/local/bin/sway-screenshare "$pkgdir/usr/local/bin/sway-screenshare"
    install -Dm 0755 desktop/usr/local/bin/sway-screenshot-area "$pkgdir/usr/local/bin/sway-screenshot-area"
    install -Dm 0755 desktop/usr/local/bin/wl-clipboard-manager "$pkgdir/usr/local/bin/wl-clipboard-manager"

    # Environment
    install -Dm 0644 desktop/etc/environment.d/20-xdg.conf "$pkgdir/etc/environment.d/20-xdg.conf"
    install -Dm 0644 desktop/etc/environment.d/60-wayland.conf "$pkgdir/etc/environment.d/60-wayland.conf"
}

package_couldinho-dev() {
    install=couldinho-dev.install

    depends=(
        couldinho-base
        couldinho-desktop
    )

    # Platforms/Languages
    depends+=(
        docker                  # Containerisation
        docker-compose          # Docker dev management
        docker-credential-pass  # Store and use docker credentials from pass
        rust                    # The rust programming language and tools
        rustup                  # Rust toolchain installer
        nasm                    # x86 Assembler
        ipython                 # iPython Notebooks
    )

    # Debugging
    depends+=(
        gdb                     # GNU debugger
        rr                      # Replaying debugger
    )

    # Cloud
    depends+=(
        aws-cli                 # Command line interface to AWS
        aws-sam-cli             # Client for AWS SAM serverless
        npm                     # Node package manager (used for serverless)
    )
}

package_couldinho-sec() {
    install=couldinho-sec.install

    depends=(
        couldinho-desktop
    )

    # Binary
    depends+=(
        010editor               # Binary editor with templates
        ltrace                  # Trace library calls
        strace                  # Trace system calls and signals
        binwalk                 # Find and extract file types within a file
    )

    # Network
    depends+=(
        nmap                    # Network mapper
        wireshark-cli           # Network analyzer - CLI tools
        wireshark-qt            # Network analyzer - GUI
        burpsuite               # Security testing web applications
        dirbuster               # Brute force directories and filenames
        wfuzz                   # Brute force web applications
    )

    # Debugging
    depends+=(
        pwndbg                  # Enhance GDB, similar to GEF
        gef-git                 # GDB Enhanced Features
        pwngdb                  # GDB for pwn
    )

    # Reversing
    depends+=(
        radare2                 # Command line reverse engineering framework
        ida-free                # Hexrays reversen engineering framework
        ghidra                  # Reverse engineering framework similar to IDA
        python-capstone         # GEF optional dependency
        python-unicorn          # GEF optional dependency
        python-keystone         # GEF optional dependency
        python-ropper           # GEF optional dependency
    )

    # Cracking
    depends+=(
        john                    # John the Ripper password cracker
    )

    # Desktop mimetype apps
    install -Dm 0644 sec/usr/local/share/applications/ida-free.desktop "$pkgdir/usr/local/share/applications/ida-free.desktop"
    install -Dm 0644 sec/usr/local/share/applications/010editor.desktop "$pkgdir/usr/local/share/applications/010editor.desktop"
    install -Dm 0644 sec/usr/local/share/applications/ghidra.desktop "$pkgdir/usr/local/share/applications/ghidra.desktop"
}

package_couldinho-laptop() {
    install=couldinho-laptop.install

    # base + desktop
    depends=(
        couldinho-desktop
    )

    # Lighting
    depends+=(
        light                   # Program to change backlight
        wlsunset                # Day/night gamma adjustments
        wluma                   # Automatic gamma adjustments based on screen content
    )

    # Network + Bluetooth
    depends+=(
        iwd                     # Internet wireless daemon
        bluez                   # Bluetooth daemons
        bluez-utils             # Utils for bluez
        pulseaudio-bluetooth    # Bluetooth module for pulseaudio
    )

    install -Dm 0644 laptop/etc/xdg/waybar/config.patch "$pkgdir/etc/xdg/waybar/config.patch"

    # Display
    #depends+=(
    #    evdi                    # Kernel module for DisplayLink
    #    displaylink             # Driver for DisplayLink
    #    displaylink-connect     # Auto-set monitor for DisplayLink
    #)

    # Overwrite desktop files
    install -Dm 0644 laptop/usr/share/X11/xorg.conf.d/40-libinput.conf "$pkgdir/usr/share/X11/xorg.conf.d/couldinho-laptop-40-libinput.conf"
    install -Dm 0644 laptop/etc/pulse/system.pa.d/auto-connect.pa "$pkgdir/etc/pulse/system.pa.d/auto-connect.pa"
    install -Dm 0644 laptop/etc/bluetooth/main.conf "$pkgdir/etc/bluetooth/couldinho-laptop-main.conf"
    install -Dm 0644 laptop/etc/sway/config.patch "$pkgdir/etc/sway/config.patch"
    install -Dm 0755 laptop/usr/local/bin/rofi-bluetooth "$pkgdir/usr/local/bin/rofi-bluetooth"
}

package_couldinho-vmware() {
    install=couldinho-vmware.install

    # base + desktop
    depends=(
        couldinho-desktop
    )

    # VMWare specific programs
    depends+=(
        light                   # Program to change backlight
        open-vm-tools           # Open source version of VMWare Tools
        xf86-input-vmmouse      # VMWare mouse driver
        xf86-video-vmware       # VMWare video driver
        gtkmm3                  # C++ bindings for GTK+ 3 - used for VMWare copy/paste
    )

    # Environment
    install -Dm 0644 vmware/etc/environment.d/70-vmware.conf "$pkgdir/etc/environment.d/70-vmware.conf"
}
