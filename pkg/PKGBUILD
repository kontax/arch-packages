pkgbase='couldinho'
pkgname=(couldinho-base couldinho-desktop)
pkgver=1
pkgrel=1
pkgdesc="Packages for couldinho systems"
arch=(x86_64)
url="https://github.com/kontax/dotfiles"
license=(MIT)
groups=(couldinho)

source=(conf-files.tar.xz)
sha256sums=(a9178408939f56fef54ef187920d376775c6b42ba96bb6decb5b5bbc1952cb1f)

package_couldinho-base() {
    install=couldinho-base.install

    install -Dm 0644 base/etc/profile.d/couldinho-base.sh "$pkgdir/etc/profile.d/couldinho-base.sh"

    # base
    depends=(
        bash
        bzip2
        coreutils
        device-mapper
        dhcpcd
        diffutils
        e2fsprogs
        file
        filesystem
        findutils
        gawk
        gcc-libs
        gettext
        glibc
        grep
        gzip
        inetutils
        iproute2
        iputils
        less
        licenses
        linux
        linux-firmware
        logrotate
        man-db
        man-pages
        mdadm
        netctl
        pacman
        pciutils
        perl
        procps-ng
        psmisc
        s-nail
        sed
        shadow
        sysfsutils
        systemd-sysvcompat
        tar
        texinfo
        usbutils
        util-linux
        vim
        which
    )

    # base-devel
    depends+=(
        autoconf
        automake
        binutils
        bison
        fakeroot
        flex
        gcc
        groff
        libtool
        m4
        make
        patch
        pkgconf
        sudo
        systemd
    )

    install -Dm 0644 base/etc/default/grub "$pkgdir/etc/default/couldinho-grub"
    install -Dm 0644 base/etc/mkinitcpio.conf "$pkgdir/etc/couldinho-mkinitcpio.conf"
    install -Dm 0644 base/etc/pacman.conf "$pkgdir/etc/couldinho-pacman.conf"
    install -Dm 0644 base/etc/pacman.d/couldinho-arch-aur "$pkgdir/etc/pacman.d/couldinho-arch-aur"
    install -Dm 0400 base/etc/sudoers "$pkgdir/etc/couldinho-sudoers"
    install -Dm 0644 base/etc/vimrc "$pkgdir/etc/couldinho-vimrc"

    # Boot and base progs
    depends+=(
        grub efibootmgr     # For EFI systems
        cryptboot           # Encrypted partitions
        intel-ucode         # Microcode patches
        fwupd               # Firmware updater
        terminus-font       # Better font for console
        git                 # Move to devel?
        cifs-utils          # Connect to shared drives
        stow                # Link dotfiles to home dir
    )

    install -Dm 0644 base/etc/gitconfig "$pkgdir/etc/gitconfig"
    
    # Base replacements
    depends+=(
        exa                 # ls replacement
        ripgrep             # Fuzzy search
        rmtrash             # Delete to bin
    )

    # Networking + VPN
    depends+=(
        networkmanager              # Networking
        networkmanager-strongswan   # IPSEC VPN
        iw                          # Move to laptop?
        wget openssh                # Download and connect
        openvpn                     # Connect to PIA
        networkmanager-openvpn      # NetworkManager drivers for OpenVPN
        private-internet-access-vpn # Scripts for managing PIA
    )
    
    install -Dm 0644 base/etc/private-internet-access/pia.conf "$pkgdir/etc/private-internet-access/pia.conf"

    # Shell
    depends+=(
        zsh                 # Better shell
        antigen-git         # Package manager
        prezto-git          # Configuration framework
        fzy                 # Fzy text selector
        lscolors-git        # Colours for ls
        pydf                # Colourised version of df
    )

    install -Dm 0644 base/etc/zsh/antigen.zsh "$pkgdir/etc/zsh/antigen.zsh"
    install -Dm 0644 base/etc/zsh/aliases.zsh "$pkgdir/etc/zsh/aliases.zsh"
    install -Dm 0644 base/etc/zsh/env.zsh "$pkgdir/etc/zsh/env.zsh"
    install -Dm 0644 base/etc/zsh/fuzzy.zsh "$pkgdir/etc/zsh/fuzzy.zsh"
    install -Dm 0644 base/etc/zsh/git.zsh "$pkgdir/etc/zsh/git.zsh"
    install -Dm 0644 base/etc/zsh/keybindings.zsh "$pkgdir/etc/zsh/keybindings.zsh"
    install -Dm 0644 base/etc/zsh/opts.zsh "$pkgdir/etc/zsh/opts.zsh"
    install -Dm 0644 base/etc/zsh/pacman.zsh "$pkgdir/etc/zsh/pacman.zsh"
    install -Dm 0644 base/etc/zsh/prezto.zsh "$pkgdir/etc/zsh/prezto.zsh"
    install -Dm 0644 base/etc/zsh/prezto-patches.zsh "$pkgdir/etc/zsh/prezto-patches.zsh"
    install -Dm 0644 base/etc/zsh/prompt.zsh "$pkgdir/etc/zsh/prompt.zsh"
    install -Dm 0644 base/etc/zsh/sandboxd.zsh "$pkgdir/etc/zsh/sandboxd.zsh"
    install -Dm 0644 base/etc/zsh/ssh.zsh "$pkgdir/etc/zsh/ssh.zsh"
    install -Dm 0644 base/etc/zsh/title.zsh "$pkgdir/etc/zsh/title.zsh"
    install -Dm 0644 base/etc/zsh/zsh-notify.zsh "$pkgdir/etc/zsh/zsh-notify.zsh"
    install -Dm 0644 base/etc/zsh/zshrc "$pkgdir/etc/zsh/couldinho-zshrc"
    install -Dm 0644 base/etc/zsh/zprezto-patches/prompt.patch "$pkgdir/etc/zsh/zprezto-patches/prompt.patch"
    install -Dm 0644 base/etc/zsh/zprezto-patches/wordchars.patch \
                 "$pkgdir/etc/zsh/zprezto-patches/wordchars.patch"

    # Filesystem
    depends+=(
        btrfs-progs         # BTRFS filesystem tools
        snapper             # Snapshot manager
        snap-pac            # Hooks for pacman for auto-snapshotting
        snap-pac-grub       # Add grub entries for snapshots
    )

    install -Dm 0644 base/etc/conf.d/snapper "$pkgdir/etc/conf.d/couldinho-snapper"
    install -Dm 0644 base/etc/snapper/configs/root "$pkgdir/etc/snapper/configs/root"
    install -Dm 0644 base/etc/snap-pac.conf "$pkgdir/etc/snap-pac.conf"
    install -Dm 0644 base/etc/default/grub-btrfs/config "$pkgdir/etc/default/grub-btrfs/couldinho-config"

    # Access Control
    depends+=(
        pass                # Text-based password manager
        pass-git-helper     # Credential helper using pass to store details
        usbguard            # USB security configuration
        yubikey-manager     # Yubikey settings
        yubico-pam          # PAM settings for yubikey
        libu2f-server       # Libraries for U2F - login with yubikey
        pam_u2f             # Login to system with U2F
        git-secret          # Helper tool for storing private files in a git repo
    )

    install -Dm 0644 base/etc/profile.d/zz_custom.sh "$pkgdir/etc/profile.d/zz_custom.sh"
    install -Dm 0600 base/etc/usbguard/usbguard-daemon.conf \
        "$pkgdir/etc/usbguard/couldinho-usbguard-daemon.conf"

    # Packages
    depends+=(
        devtools            # Package building helpers
        aurutils-git        # AUR helper
        pacman-contrib      # Contains checkupdates plus other pacman scripts
        reflector           # Automatically sort out pacman mirrorlist
    )
}

package_couldinho-desktop() {
    install=couldinho-desktop.install

    install -Dm 0644 desktop/etc/profile.d/couldinho-desktop.sh "$pkgdir/etc/profile.d/couldinho-desktop.sh"

    # base
    depends=(
        couldinho-base
    )

    # xorg
    depends+=(
        xorg-server         	# X server
        xorg-xfontsel       	# Selecting X11 fonts easily
        xorg-xinit          	# Start up X and various WM's
        xorg-xprop          	# Display X properties
        xorg-xrandr         	# Resize & Rotate
    )

    # i3
    depends+=(
        i3-gaps                 # i3 window manager with gaps between windows
        i3status                # Status bar for i3
        py3status               # Manage i3status through python
        i3ipc-python            # IPC for i3 via python
    )

    install -Dm 0644 desktop/etc/i3/config "$pkgdir/etc/i3/couldinho-config"
    install -Dm 0644 desktop/etc/i3status.conf "$pkgdir/etc/couldinho-i3status.conf"
    install -Dm 0644 desktop/usr/share/fonts/taskbar.ttf "$pkgdir/usr/share/fonts/taskbar.ttf"

    # Login manager
    depends+=(
        lightdm             	# Login manager
        lightdm-gtk-greeter 	# Theme for login manager
    )

    install -Dm 0644 desktop/etc/lightdm/lightdm.conf "$pkgdir/etc/lightdm/couldinho-lightdm.conf"
    install -Dm 0644 desktop/etc/lightdm/lightdm-gtk-greeter.conf \
        "$pkgdir/etc/lightdm/couldinho-lightdm-gtk-greeter.conf"

    # Further i3 packages
    depends+=(
        rofi                	# dmenu replacement
        rofi-calc           	# Wrapper for qalculate
        libqalculate        	# More powerful calculator
        rofi-dmenu          	# Wrapper for things depending on dmenu binary
        ttf-inconsolata     	# Font for terminal and status etc.
        otf-font-awesome    	# Font with icons
        python-tzlocal      	# Used within the clock for the status bar
        gsimplecal          	# GTK calendar for the status bar
        dunst               	# Notification library
        python-pydbus           # Python DBus connector (used by usbguard i3status module)
        yubikey-touch-detector  # i3status notification for when yubikey needs attention 
        pinentry-dmenu          # Enter GPG pins using dmenu
    )

    # Multimedia
    depends+=(
        alsa-utils          	# Sound support
        pulseaudio          	# Sound server
        pulseaudio-alsa     	# ALSA config for pulseaudio
        pamixer             	# Command line mixer
    )

    # Terminal + desktop tools
    depends+=(
        kitty               	# GPU enhanced terminal emulator
        flameshot           	# Screenshot tool
        compton             	# X compositor
        copyq               	# Copy/Paste utility
        unclutter           	# Hide mouse in i3 when not in use
        ranger              	# File manager from terminal (vim keybindings)
        python-pillow       	# Used to view pictures within ranger
        freerdp             	# Remote desktop client
    )

    install -Dm 0644 desktop/etc/xdg/kitty/diff.conf "$pkgdir/etc/xdg/kitty/diff.conf"
    install -Dm 0644 desktop/etc/xdg/kitty/kitty.conf "$pkgdir/etc/xdg/kitty/kitty.conf"
    install -Dm 0644 desktop/etc/xdg/kitty/vm.py "$pkgdir/etc/xdg/kitty/vm.py"
    install -Dm 0644 desktop/etc/xdg/compton.conf "$pkgdir/etc/xdg/couldinho-compton.conf"

    # Networking
    depends+=(
        nm-connection-editor        # Gui tool for managing NetworkManager connections
        networkmanager-dmenu-git    # dmenu for NetworkManager connections
    )

    # Web
    depends+=(
        vivaldi                 # Browser
        vivaldi-ffmpeg-codecs   # Browser video codecs
        vivaldi-widevine        # Stream DRM support for vivaldi
        pepper-flash            # Flash player
        x265                    # Video compression
        browserpass             # Browser extenson for pass
    )

    # General scripts
    install -Dm 0755 desktop/usr/local/bin/i3-autoname-workspaces "$pkgdir/usr/local/bin/i3-autoname-workspaces"
    install -Dm 0755 desktop/usr/local/bin/i3exit "$pkgdir/usr/local/bin/i3exit"
    install -Dm 0755 desktop/usr/local/bin/is_online "$pkgdir/usr/local/bin/is_online"
    install -Dm 0755 desktop/usr/local/bin/rdp "$pkgdir/usr/local/bin/rdp"
    install -Dm 0755 desktop/usr/local/bin/renumber-sessions "$pkgdir/usr/local/bin/renumber-sessions"
    install -Dm 0755 desktop/usr/local/bin/rofi-calc "$pkgdir/usr/local/bin/rofi-calc"
    install -Dm 0755 desktop/usr/local/bin/set-brightness "$pkgdir/usr/local/bin/set-brightness"
}
